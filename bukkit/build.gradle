plugins {
    id 'java'
    //id "com.github.johnrengelman.shadow" version "6.1.0"
    id 'maven-publish'
    id "com.modrinth.minotaur"
}


project.archivesBaseName = project.archives_base_name
//project.version = project.mod_version
version = project.mod_version


repositories {
    mavenCentral()

    maven {
        name = "BucketMaven"
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        name = "dmulloy2"
        url "https://repo.dmulloy2.net/repository/public/"
    }
}

configurations {

    compileModule

    compileClasspath.extendsFrom compileModule
    runtimeClasspath.extendsFrom compileModule

}

dependencies {
    implementation "org.spigotmc:spigot-api:${rootProject.spigot_version}"
    implementation "com.comphenix.protocol:ProtocolLib:${rootProject.protocollib_version}"

    compileModule(project(':emotesAPI')) { transitive = false }
    compileModule(project(':executor')) { transitive = false }
    compileModule(project(':emotesServer')) { transitive = false }
    compileModule(project(':emotesAssets')) { transitive = false }

    compileModule("dev.kosmx.player-anim:anim-core:${rootProject.player_animator_version}") {
        transitive false
    }
}

processResources{

    inputs.property "version", project.version
    inputs.property "description", rootProject.mod_description

    filesMatching("plugin.yml"){
        expand version: project.version, description: rootProject.mod_description
    }

}

jar{
    from{
        configurations.compileModule.collect() {it.isDirectory() ? it : zipTree(it)}
    }
    archiveClassifier.set("bukkit")
}

task copyArtifacts{
    dependsOn('build')
    doLast {
        copy{
            from "${project.buildDir}/libs/${project.archives_base_name}-${rootProject.mod_version}-bukkit.jar"
            into "${rootProject.projectDir}/artifacts"
        }
    }
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven

            artifactId = 'emotesBukkit'

            artifact(jar) {
                classifier ''
            }
            artifact(sourcesJar)


            pom{
                name = "emotesBukkit"
                description = "Minecraft Emotecraft Bukkit plugin"
                url = 'https://github.com/KosmX/emotes'
                developers {
                    developer {
                        id = 'kosmx'
                        name = 'KosmX'
                        email = 'kosmx.mc@gmail.com'
                    }
                }

                licenses{
                    license{
                        name = "GPL3"
                        url = "https://github.com/KosmX/emotes/blob/HEAD/LICENSE"
                    }
                }

                scm {
                    connection = 'scm:git:github.com/kosmx/emotes.git'
                    developerConnection = 'scm:git:github.com/kosmx/emotes.git'
                    url = 'https://github.com/KosmX/emotes'
                }
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        if (project.keysExists) {
            repositories {
                maven {
                    url = 'https://maven.kosmx.dev/'
                    credentials {
                        username = 'kosmx'
                        password = project.keys.kosmx_maven
                    }
                }
            }
        }
        else {
            mavenLocal()
        }
    }
}

if (keysExists) {
    modrinth {
        versionType = project.cfType
        uploadFile = jar

        token = project.keys.modrinth_token
        projectId = "pZ2wrerK"
        versionNumber = "${project.mod_version}+${project.minecraft_version}-bukkit"
        versionName = "${project.mod_version}"

        gameVersions = ["1.21.1"]
        changelog = changes
        loaders = ["bukkit", "folia", "paper", "purpur", "spigot"]
        failSilently = false
    }
}
