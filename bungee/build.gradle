plugins {
    id 'java'
    id "com.modrinth.minotaur"
}

version 'unspecified'

project.archivesBaseName = project.archives_base_name
//project.version = project.mod_version
project.version = project.mod_version

repositories {
    mavenCentral()
    
    maven{
        name = "BungeeMaven"
        url "https://hub.spigotmc.org/nexus/content/repositories/public/"
    }
}

configurations {
    compileModule

    compileClasspath.extendsFrom compileModule
    runtimeClasspath.extendsFrom compileModule
}

dependencies {
    compileOnly "net.md-5:bungeecord-api:${rootProject.bungeecord_version}"

    compileModule(project(':emotesAPI')){ transitive = false }
    compileModule(project(':executor')){ transitive = false }
    compileModule(project(':emotesServer')){ transitive = false }
    compileModule(project(':emotesAssets')){ transitive = false }

    compileModule("dev.kosmx.player-anim:anim-core:${rootProject.player_animator_version}") {
        transitive false
    }
}

processResources{

    inputs.property "version", project.version

    filesMatching("bungee.yml"){
        expand version: project.version
    }

}

jar{
    from{
        configurations.compileModule.collect() {it.isDirectory() ? it : zipTree(it)}
    }
    archiveClassifier.set("bungee")
}

task copyArtifacts{
    dependsOn('build')
    doLast {
        copy{
            from "${project.buildDir}/libs/${project.archives_base_name}-${project.mod_version}-bungee.jar"
            into "${rootProject.projectDir}/artifacts"
        }
    }
}

if (keysExists) {
    modrinth {
        versionType = project.cfType
        uploadFile = jar

        token = project.keys.modrinth_token
        projectId = "pZ2wrerK"
        versionNumber = "${project.mod_version}+${project.minecraft_version}-bungee"
        versionName = "${project.mod_version}"

        gameVersions = ["1.21.1"]
        changelog = changes
        loaders = ["waterfall", "bungeecord"]
        failSilently = false
    }
}
