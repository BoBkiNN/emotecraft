plugins {
    id 'java'
    id "com.modrinth.minotaur"
}

version 'unspecified'

project.archivesBaseName = project.archives_base_name
project.version = project.mod_version

repositories {
    mavenCentral()

    maven {
        name = 'velocitypowered-repo'
        url = 'https://nexus.velocitypowered.com/repository/maven-public/'
    }
}

configurations {
    compileModule

    compileClasspath.extendsFrom compileModule
    runtimeClasspath.extendsFrom compileModule
}

dependencies {
    compileOnly "com.velocitypowered:velocity-api:${rootProject.velocity_version}"
    annotationProcessor "com.velocitypowered:velocity-api:${rootProject.velocity_version}"

    compileModule(project(':emotesAPI')){ transitive = false }
    compileModule(project(':executor')){ transitive = false }
    compileModule(project(':emotesServer')){ transitive = false }
    compileModule(project(':emotesAssets')) { transitive = false }

    compileModule("dev.kosmx.player-anim:anim-core:${rootProject.player_animator_version}") {
        transitive false
    }
}

jar {
    from {
        configurations.compileModule.collect() { it.isDirectory() ? it : zipTree(it) }
    }
    archiveClassifier.set("velocity")
}

task copyArtifacts {
    dependsOn('build')
    doLast {
        copy {
            from "${project.buildDir}/libs/${project.archives_base_name}-${project.mod_version}-velocity.jar"
            into "${rootProject.projectDir}/artifacts"
        }
    }
}

if (keysExists) {
    modrinth {
        versionType = project.cfType
        uploadFile = jar

        token = project.keys.modrinth_token
        projectId = "pZ2wrerK"
        versionNumber = "${project.mod_version}+${project.minecraft_version}-velocity"
        versionName = "${project.mod_version}"

        gameVersions = ["1.21.1"]
        changelog = changes
        loaders = ["velocity"]
        failSilently = false
    }
}
