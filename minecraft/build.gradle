plugins {
    id "architectury-plugin"
    id "com.matthewprenger.cursegradle"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = "${project.archives_base_name}-for-MC${project.minecraft_version}"
    version = rootProject.mod_version
}

task buildAll{
    dependsOn(":minecraft:fabric:build")
    dependsOn(":minecraft:neoforge:build")
}

task copyArtifacts{
    dependsOn("buildAll")
    doLast {
        copy{
            from "${project.projectDir}/fabric/build/libs/${project.archivesBaseName}-${project.version}.jar"
            into "${rootProject.projectDir}/artifacts"
        }

        copy{
            from "${project.projectDir}/neoforge/build/libs/${project.archivesBaseName}-${project.version}-neoforge.jar"
            into "${rootProject.projectDir}/artifacts"
        }
    }
}

if(keysExists) {
    task publishMod {
        finalizedBy(":minecraft:fabric:modrinth")
        finalizedBy(":minecraft:neoforge:modrinth")
        finalizedBy(":minecraft:publishToCF")
    }

    curseforge {
        apiKey = project.keys.curseforge_key

        project {
            id = '397809' //Fabric version
            changelogType = "markdown"
            //changelog = '[See on Github](https://github.com/KosmX/emotes/commits/master)'
            changelog = changes
            releaseType = project.cfType
            addGameVersion rootProject.minecraft_version
            addGameVersion "Fabric"
            addGameVersion "Quilt"

            relations {
                requiredDependency 'fabric-api'
                embeddedLibrary 'playeranimator'
            }

            options {
                forgeGradleIntegration = false // FABRIC MOD
                javaVersionAutoDetect = false // defaults to true
            }

            mainArtifact("${project.getProjectDir()}/fabric/build/libs/${project.archivesBaseName}-${project.version}-fabric.jar") {
                //displayName = archivesBaseName
            }
        }

        project {
            id = '403422' //Fabric version
            changelogType = "markdown"
            //changelog = '[See on Github](https://github.com/KosmX/emotes/commits/master)'
            changelog = changes
            releaseType = project.cfType
            addGameVersion rootProject.minecraft_version
            addGameVersion "NeoForge"

            relations {
                embeddedLibrary 'playeranimator'
            }

            options {
                forgeGradleIntegration = false // ARCHITECTURY MOD
                javaVersionAutoDetect = false // defaults to true
            }

            mainArtifact("${project.getProjectDir()}/neoforge/build/libs/${project.archivesBaseName}-${project.version}-neoforge.jar") {
                //displayName = archivesBaseName
            }
        }//*/

    }

    task publishToCF {
        dependsOn('buildAll')
        finalizedBy(tasks.curseforge)
    }
}
